apply plugin: 'kotlin-platform-js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'com.moowork.node'

dependencies {
    expectedBy project(':common')

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-js', version: kotlinVersion

    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-js', version: kotlinVersion
}

node {
    download true
}

compileKotlin2Js {
    kotlinOptions {
        metaInfo = true
        sourceMap = true
        sourceMapEmbedSources = 'always'
        moduleKind = 'umd'
        main = 'com.github.czyzby.example.client.Main'
    }
}

compileTestKotlin2Js {
    kotlinOptions.moduleKind = 'umd'
}

// Downloads JS dependencies:
task yarnInstall(type: YarnTask) {
    args = ['install']
}

// Creates minified, packed main.bundle.js at build/dist:
task bundle(type: YarnTask, dependsOn: [runDceKotlinJs, yarnInstall]) {
    args = ["run", "bundle"]
}

// Starts dev server that serves built application; reloads after running runDceKotlinJs:
task run(type: YarnTask, dependsOn: [runDceKotlinJs, yarnInstall]) {
    args = ["run", "start"]
}

// Copies files from src/main/resouces to build/dist. These resources will be served by dev server:
task copyStaticResources(type: Copy) {
    from sourceSets.main.resources
    into "${buildDir}/dist"
}

// Extracts JS libs from included dependencies to node_modules in build directory:
task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

// Test runner:
task runKarma(type: YarnTask, dependsOn: [populateNodeModules, yarnInstall]) {
    args = ['test']
}

test.dependsOn runKarma
assemble.dependsOn bundle
bundle.dependsOn copyStaticResources
run.dependsOn copyStaticResources

afterEvaluate {
    if (tasks.findByName('unpackDependenciesTestKotlinJs')) {
        tasks.unpackDependenciesTestKotlinJs.enabled = false
    }
}
